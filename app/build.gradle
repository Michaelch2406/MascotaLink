plugins {
    alias(libs.plugins.android.application)
    id 'com.google.gms.google-services'
    alias(libs.plugins.hilt.android)
    id "org.sonarqube" version "7.0.1.6134"
}

android {
    namespace 'com.mjc.mascotalink'
    compileSdk 36

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        applicationId "com.mjc.mascotalink"
        minSdk 32
        targetSdk 34
        versionCode 1
        versionName "1.0"

        // Leer la clave de API desde local.properties y asignarla a manifestPlaceholders
        def localProperties = new Properties()
        def localPropertiesFile = rootProject.file('local.properties')
        if (localPropertiesFile.exists()) {
            localProperties.load(new FileInputStream(localPropertiesFile))
        }
        manifestPlaceholders = [MAPS_API_KEY: localProperties.getProperty('mapsApiKey', "")]
        buildConfigField "String", "MAPS_API_KEY", "\"${localProperties.getProperty('mapsApiKey', "")}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Solo ABI de destino del dispositivo: reduce tamaño y elimina x86/x86_64
        ndk {
            abiFilters "arm64-v8a", "x86_64"
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = "Walki-debug.apk"
                }
            }
        }
        release {
            // Optimizaciones de APK: R8 + Resource Shrinking
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = "Walki.apk"
                }
            }
        }
    }
    lint {
        baseline = file("lint-baseline.xml")
        abortOnError false
    }
    lintOptions{
        checkReleaseBuilds false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    // ========== NETWORKING ==========
    implementation libs.socketio
    implementation libs.retrofit
    implementation libs.retrofit.gson
    implementation libs.okhttp
    implementation libs.okhttp.logging

    // ========== FIREBASE ==========
    implementation platform(libs.firebase.bom)
    implementation libs.firebase.analytics
    implementation libs.firebase.auth
    implementation libs.firebase.firestore
    implementation libs.firebase.storage         // <- subir fotos, docs, videos
    implementation libs.firebase.messaging       // <- push notifications
    implementation libs.firebase.functions       // <- llamadas a Cloud Functions (opcional)
    implementation libs.firebase.ui.auth         // <- facilita login/registro con providers

    // ========== GOOGLE PLAY SERVICES ==========
    // Maps & Location
    implementation libs.play.services.maps
    implementation libs.maps.utils
    implementation libs.play.services.location
    implementation libs.places
    implementation libs.geofire

    // ML Kit vía Google Play services (evita incluir .so en el APK)
    implementation libs.play.services.mlkit.face
    implementation libs.play.services.mlkit.text

    // ========== CAMERAX ==========
    implementation libs.camerax.core
    implementation libs.camerax.camera2
    implementation libs.camerax.lifecycle
    implementation libs.camerax.view
    implementation libs.camerax.video

    // ========== SECURITY ==========
    implementation libs.security.crypto
    implementation libs.biometric

    // ========== IMAGE LOADING ==========
    implementation libs.glide
    implementation libs.circleimageview
    annotationProcessor libs.glide.compiler

    // ========== ANDROIDX CORE ==========
    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    implementation libs.recyclerview
    implementation libs.swiperefreshlayout

    // ========== LIFECYCLE & VIEWMODEL ==========
    implementation libs.lifecycle.viewmodel
    implementation libs.lifecycle.livedata
    implementation libs.work.runtime

    // ========== DEPENDENCY INJECTION ==========
    implementation libs.hilt.android
    annotationProcessor libs.hilt.compiler

    // ========== PDF GENERATION ==========
    implementation libs.itext7

    // ========== COMPOSE (solo para icons) ==========
    implementation libs.compose.material.icons

    // Google AI (Gemini)
    implementation("com.google.ai.client.generativeai:generativeai:0.9.0")

    // Testing
    testImplementation libs.junit
    testImplementation libs.mockito
    testImplementation libs.arch.core.testing
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}